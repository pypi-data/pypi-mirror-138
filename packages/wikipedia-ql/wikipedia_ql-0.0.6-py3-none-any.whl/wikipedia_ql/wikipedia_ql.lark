query: "from"i _S* source _S* selectors_group

source: ( _source_type ":" )? STRING

_source_type: IDENT

// TODO: closest child: foo > bar

nested_selectors: (_SM* ">>" _SM* selectors | _SM* follow_link)

?selectors: selector | selectors_group
?selectors_group: "{" _SM* selector _S* (";" _SM* selector)* ";" ?  _SM* "}"
selector: _selector_expression attribute_selector? as_named? _S* nested_selectors?

follow_link: "->" _SM* selectors_group

// TODO: and/or type
as_named: _S+ "as"i _S+ STRING

_selector_expression: custom_selector
                      | css_selector
                      | attribute_selector

custom_selector: WIKIPEDA_QL_SELECTOR_NAME (attrib | custom_function)*

WIKIPEDA_QL_SELECTOR_NAME: "text-group" | "text" | "sentence" | "section" | "page" | "table-data"

custom_function: ":" IDENT ("(" _S* ( IDENT | NUMBER | STRING ) _S* ")")?

attribute_selector: "@" IDENT

// https://www.w3.org/TR/CSS21/grammar.html
css_selector: element_name (HASH | class | attrib | pseudo)* | (HASH | class | attrib | pseudo)+
element_name: IDENT | "*"
class: "." IDENT
attrib: "[" _S* IDENT _S* ( ATTR_OP _S* ( IDENT | STRING | NUMBER ) _S* )? "]"
pseudo: ":" IDENT ("(" _S* ( IDENT | NUMBER ) _S* ")")?

ATTR_OP: "=" | INCLUDES | DASHMATCH | ENDSWITH | STARTSWITH

FUNCTION: IDENT "("
INCLUDES: "~="
DASHMATCH: "|="
ENDSWITH: "$="
STARTSWITH: "^="
HASH: "#" NAME
IDENT:   "-"? NMSTART NMCHAR*
NAME:    NMCHAR+
NONASCII:  /[\240-\377]/
UNICODE:   /\\[0-9a-f]{1,6}(\r\n|[ \t\r\n\f])?/
ESCAPE:    UNICODE | /\\[^\r\n\f0-9a-f]/
NMSTART:   /[_a-z]/ | NONASCII | ESCAPE
NMCHAR:    /[_a-z0-9-]/ | NONASCII | ESCAPE

NUMBER: /[0-9]+/

_S: (" "|/\t/)+
_SM: /[ \t\f\r\n]/+

%import common.ESCAPED_STRING -> STRING
%import common.INT

// FIXME: Doesn't work somehow :(
%ignore _S
%ignore _SM
